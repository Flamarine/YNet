apply plugin: 'fabric-loom'
apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8
archivesBaseName = "${project.mod_name}-${project.mod_version}-${project.minecraft_version}"

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modCompileOnly "TechReborn:${project.tr_slug}"
    modImplementation "com.github.vini2003:spinnery:${project.spinnery_version}"
}

repositories {
    maven {
        name = "TechReborn"
        url = "https://maven.fabricmc.net/"
    }
    maven {
        name = "Spinnery"
        url  "https://dl.bintray.com/spinnery/Spinnery"
    }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

setBuildDir("../../build")

jar {
    from "LICENSE"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task apiJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("api")
    from sourceSets.main.allSource.filter { it.path.contains("/api/") }
}

jar.dependsOn apiJar
jar.dependsOn sourcesJar
build.dependsOn jar
